#Version: v0.2
#Don't change these (^*) lines!
import:
	java.lang.System
	java.io.BufferedReader
	java.io.InputStreamReader
	java.net.URL
	me.clip.placeholderapi.PlaceholderAPI
expression region of %player%:
	get:
		loop regions at expression-1:
			set {_s} to "%loop-value%"
			set {_s::*} to {_s} parsed as "%text% in %text%"
			add {_s::1} to {_return::*}
		return {_return::*}
expression placeholder parsed %$strings% [from %-player%]:
	return type: string
	get:
		loop expressions-1:
			add PlaceholderAPI.setPlaceholders(expression-2, loop-value) to {_return::*}
		return {_return::*}
command /skboard [<text="help">] [<text>] [<text>] [<text>]:
	trigger:
		if executor has permission "%{SkriptBoard::adminPerm}%":
			if arg-1 is "help":
				send "%{SkriptBoard::helpCMD::*}%"
			else if arg-1 is "reload" or "rl":
				if arg-2 isn't set:
					set {_systemMS} to current system milliseconds
					if dir "plugins/SkriptBoard/" doesn't exist:
						create dir "plugins/SkriptBoard/scoreboards/" 
					execute executor command "skboard reload config"
					loop files in directory "plugins/SkriptBoard/scoreboards":
						if first character of name of file loop-value isn't "-":
							load yaml "plugins/SkriptBoard/scoreboards/%name of file loop-value%.yml" as "SkriptBoard/scoreboards/%name of file loop-value%.yml"
							save yaml "SkriptBoard/scoreboards/%name of file loop-value%.yml" without extra lines between nodes
							add 1 to {_scoreboardAmount}
					loop loaded yaml:
						if loop-value contains "SkriptBoard/scoreboards/":
							if file "plugins/%loop-value%" doesn't exist:
								unload yaml loop-value
					send "%{SkriptBoard::prefix}% &2Succesfully reloaded with &e%{_scoreboardAmount}% &2scoreboard. &8(&e%difference between {_systemMS} and current system milliseconds% ms&8)" to executor
				else if yaml "SkriptBoard/scoreboards/%arg-2%.yml" is loaded:
					load yaml "plugins/SkriptBoard/scoreboards/%arg-2%.yml" as "SkriptBoard/scoreboards/%arg-2%.yml"
					save yaml "SkriptBoard/scoreboards/%arg-2%.yml" without extra lines between nodes
					send "%{SkriptBoard::prefix}% &2Scoreboard &e%arg-2% &2is succesfully reloaded." to executor
				else if arg-2 is "config":
					load yaml "plugins/SkriptBoard/config.yml" as "SkriptBoard/config"
					set {SkriptBoard::adminPerm} to yaml value "admin-permission" from "SkriptBoard/config"
					set {SkriptBoard::prefix} to yaml value "prefix" from "SkriptBoard/config"
					set {SkriptBoard::noPermMSG} to yaml value "no-permission-message" from "SkriptBoard/config"
					set {SkriptBoard::helpCMD::*} to colored join yaml list "help-command" from "SkriptBoard/config" with nl
					replace "%%prefix%%" with {SkriptBoard::prefix} in {SkriptBoard::noPermMSG}
					replace "%%prefix%%" with {SkriptBoard::prefix} in {SkriptBoard::helpCMD::*}
					send "%{SkriptBoard::prefix}% &eConfig &2is succesfully reloaded."
					save yaml "SkriptBoard/config" without extra lines between nodes
				else:
					send "%{SkriptBoard::prefix}% &cThere is no scoreboard with this ID."
function sendScoreboard(player: player, t: text):
	setup scoreboard of {_player} with title yaml value "scoreboard.title" from "SkriptBoard/scoreboards/%{_t}%.yml"
	if {_player} has scoreboard:
		set {_slot} to 1
		loop yaml list "scoreboard.lines" from "SkriptBoard/scoreboards/%{_t}%.yml":
			set {_value} to loop-value
			set scoreboard value {_slot} of {_player} to "%placeholder parsed {_value} from {_player}%"
			add 1 to {_slot}
on join:
	wait 5 tick
	while player is online:
		if yaml value "worlds.%player's world%.scoreboard" from "SkriptBoard/config" is set:
			if yaml value "worlds.%player's world%.regions" from "SkriptBoard/config" is set:
				if region of player isn't set:
					sendScoreboard(player, yaml value "worlds.%player's world%.scoreboard" from "SkriptBoard/config")
					set {_updateTick} to yaml value "worlds.%player's world%.update-ticks" from "SkriptBoard/config"
				else:
					if yaml value "worlds.%player's world%.regions.%region of player%.scoreboard" from "SkriptBoard/config" is set:
						sendScoreboard(player, yaml value "worlds.%player's world%.regions.%region of player%.scoreboard" from "SkriptBoard/config")
						set {_updateTick} to yaml value "worlds.%player's world%.regions.%region of player%.update-ticks" from "SkriptBoard/config"
			else:
				sendScoreboard(player, yaml value "worlds.%player's world%.scoreboard" from "SkriptBoard/config")
				set {_updateTick} to yaml value "worlds.%player's world%.update-ticks" from "SkriptBoard/config"
		else:
			if yaml value "worlds.%player's world%.regions.%region of player%.scoreboard" from "SkriptBoard/config" is set:
				sendScoreboard(player, yaml value "worlds.%player's world%.regions.%region of player%.scoreboard" from "SkriptBoard/config")
				set {_updateTick} to yaml value "worlds.%player's world%.regions.%region of player%.update-ticks" from "SkriptBoard/config"
		wait "%{_updateTick}% ticks" parsed as timespan
on region exit:
	delete scoreboard of player
on region enter:
	delete scoreboard of player
on world change:
	delete scoreboard of player
on load:
	execute console command "skboard reload"
